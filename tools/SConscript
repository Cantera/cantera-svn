from buildutils import *

Import('env', 'buildTargets', 'installTargets')
localenv = env.Clone()

programs = [('cti2ctml', ['src/cti2ctml.cpp'], ['ctbase']),
            ('ck2cti', ['src/ck2cti.cpp'], ['converters','ctbase','tpx','ctcxx']),
            ('fixtext', ['src/fixtext.cpp'] , [])]

for name, src, libs in programs:
    prog = localenv.Program(target=pjoin('..', 'bin', name),
                            source=src,
                            LIBS=libs)
    inst = localenv.Install('$ct_bindir', prog)
    buildTargets.extend(prog)
    installTargets.extend(inst)

# Copy application templates
installTargets.extend(
    localenv.Install(pjoin('$ct_templdir','cxx'),
                     mglob(localenv, pjoin('templates','cxx'), '*')) +
    localenv.Install(pjoin('$ct_templdir','f90'),
                     mglob(localenv, pjoin('templates','f90'), '*')) +
    localenv.Install(pjoin('$ct_templdir','f77'),
                     mglob(localenv, pjoin('templates','f77'), '*')))

# Copy man pages
inst = localenv.Install('$ct_mandir', mglob(localenv, 'man', '*'))
installTargets.extend(inst)
