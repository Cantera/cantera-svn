from buildutils import *

Import('env','buildTargets','installTargets')

def empty(env):
    pass

def equilSetup(env):
    env.Append(CPPPATH=['#ext/f2c_libs'])

# (subdir, library name, (file extensions), (extra setup(env)))
libs = [('base', 'ctbase', ['cpp'], empty),
        ('thermo', 'thermo', ['cpp'], empty),
        ('equil', 'equil', ['cpp','c'], equilSetup),
        ('converters', 'converters', ['cpp'], empty),
        ('numerics', 'ctnumerics', ['cpp'], empty),
        ('kinetics', 'kinetics', ['cpp'], empty),
        ('transport', 'transport', ['cpp'], empty),
        ('spectra', 'ctspectra', ['cpp'], empty),
        ('oneD', 'oneD', ['cpp'], empty),
        ('zeroD', 'zeroD', ['cpp'], empty),
        ]

for subdir, libname, extensions, setup in libs:
    localenv = env.Clone()
    setup(localenv)
    for header in mglob(localenv, subdir, 'h'):      
        h = localenv.Command('../include/cantera/kernel/%s' % header.name, header,
                             Copy('$TARGET', '$SOURCE'))
        buildTargets.extend(h)
        inst = localenv.Install(pjoin('$ct_incdir', 'kernel'), h)
        installTargets.extend(inst)

    lib = localenv.Library(pjoin('../lib', libname),
                           source=mglob(localenv, subdir, *extensions))
    inst = localenv.Install('$ct_libdir', lib)
    buildTargets.extend(lib)
    installTargets.extend(inst)
