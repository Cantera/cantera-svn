from buildutils import *

Import('env', 'buildTargets', 'installTargets')
localenv = env.Clone()

programs = [('cti2ctml', ['cti2ctml.cpp']),
            ('ck2cti', ['ck2cti.cpp'])]

localenv.Append(CPPPATH=['#src', '#include'])

for name, src in programs:
    prog = localenv.Program(target=pjoin('#build/bin', name),
                            source=src,
                            LIBS=['cantera'])

    if localenv['addInstallTargets']:
        inst = localenv.Install('$inst_bindir', prog)
        buildTargets.extend(prog)
        installTargets.extend(inst)

# Copy man pages
if localenv['addInstallTargets'] and env['INSTALL_MANPAGES']:
    inst = localenv.Install('$inst_mandir', mglob(localenv, 'man', '*'))
    installTargets.extend(inst)
