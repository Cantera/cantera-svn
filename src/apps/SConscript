from buildutils import *

Import('env', 'buildTargets', 'installTargets')
localenv = env.Clone()

programs = [('cti2ctml', ['cti2ctml.cpp'], ['ctbase']),
            ('ck2cti', ['ck2cti.cpp'], ['converters','ctbase','tpx'])]

localenv.Append(CPPPATH=['#src', '#include'])

for name, src, libs in programs:
    prog = localenv.Program(target=pjoin('#build/bin', name),
                            source=src,
                            LIBS=libs)
    inst = localenv.Install('$inst_bindir', prog)
    buildTargets.extend(prog)
    installTargets.extend(inst)

# Copy man pages
if env['INSTALL_MANPAGES']:
    inst = localenv.Install('$inst_mandir', mglob(localenv, 'man', '*'))
    installTargets.extend(inst)
